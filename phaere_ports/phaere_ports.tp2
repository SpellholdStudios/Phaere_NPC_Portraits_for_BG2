BACKUP ~phaere_ports/backup~
//AUTHOR ~rastor@rpgdungeon.net~
SUPPORT ~Spellhold Studios at http://www.shsforums.net/forum/127-mod-resurrections/~

VERSION ~v5.0~

README ~phaere_ports/readme/phaere_ports-readme-english.html~

ALWAYS

	ACTION_IF NOT VARIABLE_IS_SET is_ee BEGIN // Always block must run only once per install!

		// Determines which game is installed and if the game is an Enhanced Edition.
		OUTER_SET is_ee = (GAME_IS ~bg2ee eet~) ? 1 : 0	// EE game

		// Adds ToB trigger/actions to SoA games.
		INCLUDE ~phaere_ports/lib/tob2soa.tph~

		// Adds new portrait installation process.
		INCLUDE ~phaere_ports/lib/gw_install_portraits.tpa~

	END

END // of ALWAYS BLOCK


LANGUAGE ~English~
         ~english~
		 ~phaere_ports/lang/english/setup.tra~

LANGUAGE ~Francais~
         ~french~
		 ~phaere_ports/lang/french/setup.tra~


/* ================ *
 *  Minsc Portrait  *
 * ================ */
BEGIN @0
DESIGNATED 0 LABEL ~phaere_ports_minsc~
REQUIRE_PREDICATE GAME_IS ~bg2 tob bgt bg2ee eet~ @21 /* You must have BG2, ToB, BGT, BG2:EE or EET installed to install this component. */

LAF ~gw_install_portraits~ STR_VAR npc = minsc spell = "r#ppmnsc" END

COPY_EXISTING ~npcminsc.2da~ ~override~
	REPLACE_TEXTUALLY ~*NMINSCL~ ~*R#MINSCL~
BUT_ONLY


/* ================ *
 *  Aerie Portrait  *
 * ================ */
BEGIN @1
DESIGNATED 1 LABEL ~phaere_ports_aerie~
REQUIRE_PREDICATE GAME_IS ~bg2 tob bgt bg2ee eet~ @21

LAF ~gw_install_portraits~ STR_VAR npc = aerie spell = "r#pparie" epil = 2 END


/* ================ *
 *  Imoen Portrait  *
 * ================ */
BEGIN @2
DESIGNATED 2 LABEL ~phaere_ports_imoen~
REQUIRE_PREDICATE GAME_IS ~bg2 tob bgt bg2ee eet~ @21

LAF ~gw_install_portraits~ STR_VAR npc = imoen spell = "r#ppimmy" END


/* ================ *
 *  Edwin Portrait  *
 * ================ */
BEGIN @3
DESIGNATED 3 LABEL ~phaere_ports_edwin~
REQUIRE_PREDICATE GAME_IS ~bg2 tob bgt bg2ee eet~ @11

LAF ~gw_install_portraits~ STR_VAR npc = edwin spell = "r#ppeddy" END


/* ================== *
 *  Viconia Portrait  *
 * ================== */
BEGIN @4
DESIGNATED 4 LABEL ~phaere_ports_viconia~
REQUIRE_PREDICATE GAME_IS ~bg2 tob bgt bg2ee eet~ @21

LAF ~gw_install_portraits~ STR_VAR npc = viccy cre = viconi spell = "r#ppvicc" epil = 2 script = viconia END


/* ===================== *
 *  Haer'Dalis Portrait  *
 * ===================== */
BEGIN @5
DESIGNATED 5 LABEL ~phaere_ports_haerdalis~
REQUIRE_PREDICATE GAME_IS ~bg2 tob bgt bg2ee eet~ @21

LAF ~gw_install_portraits~ STR_VAR npc = haer spell = "r#pphaer" script = haerdali END


/* ============================= *
 *  Sarevok Portrait (ToB only)  *
 * ============================= */
BEGIN @6
DESIGNATED 6 LABEL ~phaere_ports_sarevok~
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @22	// ~You must have Throne of Bhaal, BGT, BG2:EE or EET installed to install this component.~

LAF ~gw_install_portraits~ STR_VAR npc = sarev cre = sarevok spell = "r#ppsare" script = sarev25 END


/* ================== *
 *  Jaheira Portrait  *
 * ================== */
BEGIN @7
DESIGNATED 7 LABEL ~phaere_ports_jaheira~
REQUIRE_PREDICATE GAME_IS ~bg2 tob bgt bg2ee eet~ @21 /* You must have BG2, ToB, BGT, BG2:EE or EET installed to install this component. */

LAF ~gw_install_portraits~ STR_VAR npc = jahei cre = jaheir spell = "r#ppjahe" epil = 2 script = jaheira END


/* ===================================== *
 *  Bodhi Portrait (original game only)  *
 * ===================================== */
BEGIN @8
DESIGNATED 8 LABEL ~phaere_ports_bodhi~
REQUIRE_PREDICATE GAME_IS ~bg2 tob bgt~ @23 /* You must have the original game (SoA, ToB or BGT) installed to install this component. */

COPY ~phaere_ports/portraits/bodhi~ ~override~

PRINT @30 // ~Patching files... This step make take a few moments.~
COPY_EXISTING_REGEXP ~.*\.cre~ ~override~
	READ_ASCII 0x34 "name"
	PATCH_IF ("%name%" STRING_COMPARE_CASE "NBODHIS" = 0) BEGIN
		WRITE_ASCII 0x34 ~R#BODHI~ #8
	END
BUT_ONLY


/* ================ *
 *  Mazzy Portrait  *
 * ================ */
BEGIN @9
DESIGNATED 9 LABEL ~phaere_ports_mazzy~
REQUIRE_PREDICATE GAME_IS ~bg2 tob bgt bg2ee eet~ @21 /* You must have BG2, ToB, BGT, BG2:EE or EET installed to install this component. */

LAF ~gw_install_portraits~ STR_VAR npc = mazzy spell = "r#ppmazz" END
